plugins {
    id 'java'
    id 'jacoco'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.adarshr.test-logger' version '4.0.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenCentral()
}

javafx {
    version = "17.0.8"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    def junitVersion = '5.8.1'

    // This provides json handling in ChatGPT API
    // https://mvnrepository.com/artifact/org.json/json
    implementation 'org.json:json:20231013'

    // https://mvnrepository.com/artifact/commons-fileupload/commons-fileupload
    implementation 'commons-fileupload:commons-fileupload:1.5'

    // This provides the JUnit API.
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    // This provides the TestEngine to actually run the tests.
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

test {
    useJUnitPlatform()
    options {
        systemProperty "test_delay_factor", System.getProperty("test_delay_factor")
        systemProperty "user_story_test", System.getProperty("user_story_test")
    }

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }
    // we are not going to test UI part so removed it from coverage check
    // ref: https://www.baeldung.com/jacoco-report-exclude#2-gradle-configuration
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                // JavaFX pages and components
                "client/components/*",

                // JavaFX view transitioner related implementation
                "client/utils/transitions/javafx/*",

                // JavaFX view transitioner
                "client/Routes.class",

                // App startup code
                "client/Main.class",
                "client/PantryPalApp.class",
            ])
        }))
    }
}

testlogger {
    theme 'mocha'
}

sourceSets {
    main.java.srcDirs = ['src']
    main.resources.srcDirs = ['resources']
    test.java.srcDirs = ['test']
    test.resources.srcDirs = ['test']
}

task runServer(type: JavaExec) {
    group = "Execution"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'server.PantryPalServer'
}

task runApp(type: JavaExec) {
    group = "Execution"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'client.Main'
}
